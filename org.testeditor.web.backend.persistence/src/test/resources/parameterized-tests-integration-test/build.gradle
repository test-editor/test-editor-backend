plugins {
    id 'org.testeditor.gradle-plugin' version '0.11'
    id 'maven'
    id 'eclipse'
}

group = 'org.testeditor.demo'
version = '1.0.0-SNAPSHOT'

// In this section you declare where to find the dependencies of your project
repositories {
    mavenLocal()
    jcenter()
    maven { url "http://dl.bintray.com/test-editor/Fixtures" }
    maven { url "http://dl.bintray.com/test-editor/maven/" }
}

// Configure the testeditor plugin
testeditor {
	languageVersion '2.3.3-SNAPSHOT'
	xtextVersion '2.15.0'
}

// configure logging within tests (see https://docs.gradle.org/current/dsl/org.gradle.api.tasks.testing.logging.TestLogging.html)
// show standard out during test to see logging output
test.testLogging.showStandardStreams = true
// make sure that assertion failures are reported more verbose!
test.testLogging.exceptionFormat = 'full'

// In this section you declare the dependencies for your production and test code
dependencies {
	compile 'org.testeditor.fixture:commons-fixture:1.2.+' 
	compile 'org.testeditor.fixture:core-fixture:4.3.+'
	compile 'org.testeditor.fixture:web-fixture:4.2.+'
	testCompile 'junit:junit:4.12'
}

sourceCompatibility = '1.10'
targetCompatibility = '1.10'

// add proxy settings to the environment, if present (e.g. in gradle.properties)
// keep in mind that gradle.properties w/i user ~/.gradle/gradle.properties might override project settings
if (System.properties.containsKey('http.proxyHost')) { // set proxy properties only if present
    test.doFirst {
        println 'Configuring System Properties for Proxy'
        systemProperty 'http.nonProxyHosts', System.properties['http.nonProxyHosts']
        systemProperty 'http.proxyHost', System.properties['http.proxyHost']
        systemProperty 'http.proxyPort', System.properties['http.proxyPort']
        systemProperty 'http.proxyUser', System.properties['http.proxyUser']
        systemProperty 'http.proxyPassword', System.properties['http.proxyPassword']
        systemProperty 'https.proxyHost', System.properties['https.proxyHost']
        systemProperty 'https.proxyPort', System.properties['https.proxyPort']
    }
}

task printTestClasspath {
	doLast {
		configurations.testRuntime.each { println it }
	}
}
